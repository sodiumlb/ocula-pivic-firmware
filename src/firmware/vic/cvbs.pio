/*
 * Copyright (c) 2025 Sodiumlightbaby
 *
 * SPDX-License-Identifier: BSD-3-Clause
 */

; Composite video synthesizer
; Assumes 5 bit DAC output
; Running frequency needs to be a multiple of the colour burst frequency and
; with enough over-sampling to implement the phase shifts needed
; PAL timing:  clk_sys=159.6MHz colour burst=clk_sys/36=4.433333MHz
; Commands auto pulled from the fifo
;  26                              0 
; |post-delay|L1|Repeat|L0|Pre-delay|
;      4       5    9    5    4      bits
; DC,L0,L1 are DAC levels to output
; Assuming square wave output - L0 and L1 are asserted for half periods each
; Set both to same level to represents a DC level
; Currently repeat loop tuned for 36 cycle period
; Repeat is exact so 0 equals 1 periods, 1 equals 2 periods etc
.program cvbs
.clock_div 2.0

; autopull required
    out x 4             ;get delay value (for phase change)
.wrap_target
 predelay:
    jmp x-- predelay
    ; first cycle has value extraction integrated
    mov pins osr        ;L0 out
    out x 5        [11] ;Keep L0 in x
    out y 9             ;Keep repeat in y
    out isr 5           ;Keep L1 in isr
    jmp !y bypass       ;Bypass repeat if zero
    jmp y-- first  [ 1] ;Decrement y (always taken), balance branches
    ; repeat loop
repeat:
    nop            [ 1]
first:
    mov pins isr   [17] ;L1 out
    mov pins x          ;L0 out
    jmp y-- repeat [14]
    ;last cycle has value extraction integrated
bypass:
    out y 4             ;get post delay <- autopull after here
    out x 4             ;get next pre-delay
    mov pins isr        ;L1 out
postdelay:
    jmp y-- postdelay
.wrap


; NTSC CVBS synthesizer
; A set of different commands selected with the lower 5 bits in each
; command word coming in

.program cvbs_ntsc
.clock_div 1.0
.origin 0

; DC RUN Command
; Output a DC level for repeat+2 number of dot clock cycles
; 31                  0 
; |repeat|DC|id_dc_run|
;     20   7     5     bits
public cvbs_cmd_dc_run:
    out pins 7          ;DC level out
    out y 20            ;Repeat count to y
dc_loop:
    wait 1 irq 1        ;Wait for dotclock
    jmp y-- dc_loop     ;loop y times
    wait 1 irq 1        ;Wait for dotclock again
    out pc 5            ;Next command (new word)

; PIXEL Command
; Output one set of pre-calulated levels and delays during one dot clock
; Skips third phase (second output of L0) if delay1==0
; 31                              0 
; |delay1|L1|delay0|L0|id_pixel|
;     7    7    6    7    5    bits
public cvbs_cmd_pixel:
    mov pins osr       ;Out L0
    out isr 7          ;L0 in isr
    out x 6            ;delay0 in x
delay0:
    jmp x-- delay0
    out pins 7         ;Out L1
    out x 7            ;delay1 in x
    jmp !x tail        ;Skip second L0 output if delay1 = zero
delay1:
    jmp x-- delay1
    mov pins isr       ;Out L0 again
public entry:          ;Also entry point
tail:           
    wait 1 irq 1       ;Wait for dotclock again
    out pc 5           ;Next command (new word)

; BURST Command
; Output a train of L0/L1 colour burst periods, ending with DC 
; NOTE: Only synched to dot clock at the end - timing calculations needed
; 31                                0 
; |DC|L1|L0|predelay|id_burst|
;   7  7  7     6       5    bits
public cvbs_cmd_burst:
    set y 16           ;repeat count
    out x 6            ;get predelay
    out isr 7          ;keep L0 in isr
predelay_burst:
    jmp x-- predelay_burst
burst_loop:
    mov pins isr  [21] ;L0 out
    nop           [21]
    mov pins osr  [20] ;L1 out
    nop           [21]
    jmp y-- burst_loop
    out null 7         ;Dump L1
    out pins 7         ;DC out, trigger autopull
    irq clear 1        ;Make sure we are able to sync
    wait 1 irq 1       ;Wait for dotclock
    out pc 5           ;Next command (new word)


; PAL CVBS synthesizer
; A set of different commands selected with the lower 5 bits in each
; command word coming in

.program cvbs_pal
.clock_div 1.0
.origin 0

; DC RUN Command
; Output a DC level for repeat+2 number of dot clock cycles
; 31                  0 
; |repeat|DC|id_dc_run|
;     20   7     5     bits
public cvbs_cmd_dc_run:
    out pins 7          ;DC level out
    out y 20            ;Repeat count to y
dc_loop:
    wait 1 irq 1        ;Wait for dotclock
    jmp y-- dc_loop     ;loop y times
    jmp common_tail

; PIXEL Command
; Output one set of pre-calulated levels and delays during one dot clock
; Skips third phase (second output of L0) if delay1==0
; 31                              0 
; |trunc|delay1|L1|delay0|L0|id_pixel|
;     1    6    7    6    7    5    bits
public cvbs_cmd_pixel:
    mov pins osr       ;Out L0
    out isr 7          ;L0 in isr
    out x 6            ;delay0 in x
delay0:
    jmp x-- delay0
    out pins 7         ;Out L1
    out x 6            ;delay1 in x
    out y 1            ;trunc flag in y
    jmp !x common_tail ;Skip second L0 output if delay1 = zero
delay1:
    jmp x-- delay1
    jmp !y no_truncate ;Skip truncating to blank
    set pins 9         ;Set pins to blank
    jmp common_tail
no_truncate:
    mov pins isr        ;Out L0 again if not truncate
public entry:
common_tail:            ;Also entry point
    wait 1 irq 1        ;Wait for dotclock again
    out pc 5            ;Next command (new word)


; BURST Command
; Output a train of 16 L0/L1 colour burst periods, ending with DC 
; NOTE: Only synched to dot clock at the end - timing calculations needed
; 31                                0 
; |DC|L1|L0|predelay|id_burst|
;   7  7  7     6       5    bits
public cvbs_cmd_burst:
    set y 15           ;repeat count
    out x 6            ;get predelay
    out isr 7          ;keep L0 in isr
predelay_burst:
    jmp x-- predelay_burst
burst_loop:
    mov pins isr  [17] ;L0 out
    nop           [17]
    mov pins osr  [31] ;L1 out
    irq clear 1   [ 2] ;Make sure we will be able to sync (using delay slot)
    jmp y-- burst_loop
    out null 7         ;Dump L1
    out pins 7         ;DC out, trigger autopull
    jmp common_tail