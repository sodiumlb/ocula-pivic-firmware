/*
 * Copyright (c) 2025 Sodiumlightbaby
 *
 * SPDX-License-Identifier: BSD-3-Clause
 */

; Composite video synthesizer
; Assumes 5 bit DAC output
; Running frequency needs to be a multiple of the colour burst frequency and
; with enough over-sampling to implement the phase shifts needed
; PAL timing: clk_sys=133MHz colour burst=clk_sys/30=4.433333MHz
; Commands auto pulled from the fifo
;  26                              0 
; |post-delay|L1|Repeat|L0|Pre-delay|
;      4       5    9    5    4      bits
; DC,L0,L1 are DAC levels to output
; Assuming square wave output - L0 and L1 are asserted for half periods each
; Set both to same level to represents a DC level
; Currently repeat loop tuned for 30 cycle period
; Repeat is repeat-2, so 0 equals 3 periods, 1 equals 4 periods etc
.program cvbs

; autopull required
    out x 4             ;get delay value (for phase change)
.wrap_target
 predelay:
    jmp x-- predelay
    ; first cycle has value extraction integrated
    mov pins osr        ;L0 out
    out x 5        [11] ;Keep L0 in x
    out y 9             ;Keep repeat in y
    out isr 5           ;Keep L1 in isr   
    mov pins isr   [ 1] ;L1 out
    ; repeat loop
repeat:
    nop            [12]
    mov pins x     [14] ;L0 out
    mov pins isr        ;L1 out
    jmp y-- repeat
    ;last cycle has value extraction integrated
    out y 4        [12] ;get post delay <- autopull after here
    mov pins x     [13] ;L0 out
    out x 4             ;get next pre-delay
    mov pins isr        ;L1 out
postdelay:
    jmp y-- postdelay
.wrap