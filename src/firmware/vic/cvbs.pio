/*
 * Copyright (c) 2025 Sodiumlightbaby
 *
 * SPDX-License-Identifier: BSD-3-Clause
 */

; Composite video synthesizer
; Assumes 5 bit DAC output
; Running frequency needs to be a multiple of the colour burst frequency and
; with enough over-sampling to implement the phase shifts needed
; PAL timing: clk_sys=133MHz colour burst=clk_sys/30=4.433333MHz
; Commands pulled from the fifo
;  31                                 0 
; |post-delay|L1|L0|DC|Repeat|Pre-delay|
;      4       5  5  5   10      3       bits
; DC,L0,L1 are DAC levels to output
; Assuming over-sampling - L0 and L1 are asserted only for one cycle typically representing HF peaks
; DC represents the DC level be output in between
; Currently repeat loop tuned for 30 cycle period
.program cvbs

.wrap_target
    pull noblock
    out y 3             ;get delay value (for phase change)
predelay:
    jmp y-- predelay
    out y 10            ;get repeat count
    out x 5             ;get DC
    mov isr osr         ;Keep copy of Levels in isr 
    jmp first           ;Skip L3 delay on first period
repeat:
    mov osr isr     [11];Prepare osr for new iteration + half period delay
first:
    out pins 5          ;L0 out 
    mov pins x      [13];DC out + half period delay
    out pins 5          ;L1 out
    mov pins x
    jmp y-- repeat
    out y 4
postdelay:
    jmp y-- postdelay
.wrap
